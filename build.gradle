buildscript {
    ext {
        kotlin_version = '1.4.32'
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.15'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.2"
    id "org.jetbrains.kotlin.jvm" version "1.3.0"
}

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlin-eap" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

def protobuf_version = '3.14.0'
def grpc_version = '1.33.1'
def volitionBuildNumber = System.getenv("BUILD_NUMBER") ?: '0'
def volitionSpecVersion = "1.0"
def volitionFullVersion = "$volitionSpecVersion.$volitionBuildNumber"
def volitionName = 'volition-api'

task vcpkgBootstrap(type: Exec) {
    commandLine 'vcpkg/bootstrap-vcpkg.bat', '-disableMetrics'
}

task vcpkgMakeManifest(type: Copy, dependsOn: vcpkgBootstrap){
    from file('vcpkg.json.template')
    rename("vcpkg.json.template", 'vcpkg.json')
    into './'
    filter { line -> line.replaceAll('%volitionFullVersion%', volitionFullVersion) }

    doLast {
        println(file('vcpkg.json').text)
    }
}

task vcpkgInstall(type: Exec, dependsOn: vcpkgMakeManifest) {
    commandLine 'vcpkg/vcpkg.exe', '--feature-flags=manifests,versions', 'install'
}

task vcpkgCheckVersions(type: Exec, dependsOn: vcpkgInstall) {
    commandLine 'vcpkg/vcpkg.exe', '--feature-flags=manifests,versions', 'list'
    //TODO: we should parse this table and assert the pulled version of grpc and protobuf match our constants.
}

allprojects {

    group 'com.empowerops'
    version volitionFullVersion

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlin-eap" }
    }
}

subprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'

    //This is purely for syntax highlighting in intelliJ,
    //this should be done by the gradle/proto plugin, but it isnt because they're dumb.
    sourceSets.main.java.srcDirs += 'build/generated/source/proto/main/java'
    sourceSets.main.java.srcDirs += 'build/generated/source/proto/main/grpc_java'
    javadoc { exclude "com/empowerops/volition/optimizer/**" }
}

project('api'){

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.google.protobuf'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    dependencies {
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'
        compile 'javax.annotation:javax.annotation-api:1.3.2'
        compile 'com.google.guava:guava:27.0.1-jre'

        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compile "io.grpc:grpc-netty-shaded:$grpc_version"
        compile "io.grpc:grpc-protobuf:$grpc_version"
        compile "io.grpc:grpc-stub:$grpc_version"

        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.0.0"
    }

    protobuf {

        protoc {
            artifact = "com.google.protobuf:protoc:$protobuf_version"
        }
        plugins {
            grpc_java {
                artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
            }
            grpc_csharp {
                path = "../vcpkg/packages/grpc_x64-windows/tools/grpc/grpc_csharp_plugin.exe"
            }
            grpc_cpp {
                path = "../vcpkg/packages/grpc_x64-windows/tools/grpc/grpc_cpp_plugin.exe"
            }
            grpc_python {
                path = "../vcpkg/packages/grpc_x64-windows/tools/grpc/grpc_python_plugin.exe"
            }
        }

        generateProtoTasks {
            all().each { task ->
                task.plugins {
                    grpc_java {}
                    grpc_csharp {}
                    grpc_cpp {}
                    grpc_python {}
                }
                task.builtins {
                    java { }
                    csharp {}
                    cpp {}
                    python {}
                }
            }
        }
    }

    compileJava.dependsOn vcpkgInstall

    jar {
        baseName = volitionName
        manifest {
            attributes 'Specification-Title': 'Volition API'
            attributes 'Specification-Version': volitionSpecVersion
            attributes 'Specification-Vendor': 'Empower Operations Corp'
            attributes 'Implementation-Version': volitionFullVersion
            attributes 'Implementation-Vendor': 'Empower Operations Corp'
        }
    }
    
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        baseName = volitionName
        from sourceSets.main.allSource
    }
    
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        baseName = volitionName
        from javadoc.destinationDir
    }
    
    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            Production(MavenPublication){
                groupId 'com.empowerops'
                artifactId volitionName
                version volitionFullVersion
                
                //blargwargl: we need to express transitive deps here.
                // see this: https://docs.gradle.org/current/userguide/publishing_maven.html
                
            }
        }
    }

    bintray {
        user = 'empower-operations-team'
        if (!project.hasProperty("empowerTeamBintrayApiKey")) {
            logger.info("bintray API key not set, wont be able to publish. please set empowerTeamBintrayApiKey in ~/.gradle/gradle.properties with your key from https://bintray.com/profile/edit /API key")
        }
        else {
            key = project.empowerTeamBintrayApiKey
        }

        pkg {
            repo = 'maven'
            name = volitionName
            userOrg = 'empower-operations-team'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/EmpowerOperations/volition.git'

            version {
                name = volitionFullVersion
                desc = 'Empower Operations Volition API'
                released  = new Date()
            }
        }
        configurations = ['archives']
        publications = ['Production']
    }

}

project('oasis-reference'){
    dependencies {
        compile project(":api")

        compile files("babel-0.15.jar")
        compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.9.1'

        compile "io.grpc:grpc-netty-shaded:$grpc_version"
        compile "io.grpc:grpc-protobuf:$grpc_version"
        compile "io.grpc:grpc-stub:$grpc_version"

        compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-collections-immutable', version: '0.1'
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.4.3"

        compile 'no.tornado:tornadofx:1.7.17'
        compile 'info.picocli:picocli:3.9.5'
        compile group: 'org.funktionale', name: 'funktionale-all', version: '1.2'
        implementation 'org.antlr:antlr4-runtime:4.8-1'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
        testCompile 'org.assertj:assertj-core:3.11.1'
    }


    task copyToLib(type: Copy) {
        from configurations.runtime
        into "$buildDir/deps"
    }

    assemble.dependsOn copyToLib
    
    jar {
        manifest {
            attributes 'Main-Class': 'com.empowerops.volition.ref_oasis.OptimizerCLIKt'
            attributes 'Specification-Title': 'Volition Reference Optimizer'
            attributes 'Specification-Version': volitionFullVersion
            attributes 'Specification-Vendor': 'Empower Operations Corp'
            attributes 'Implementation-Version': volitionBuildNumber
            attributes 'Implementation-Vendor': 'Empower Operations Corp'
        }
    }

    task deliverable(type: Zip, dependsOn: assemble) {
        from "$buildDir/libs"
        from "$buildDir/deps"
        from "$projectDir"
        include "*.exe"
        include "*.jar"
        archiveName "optimizer-reference-${version}.zip"
        destinationDir(file("$buildDir/deliverable"))
    }
    
    test {
        useJUnitPlatform()
    }
}