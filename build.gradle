buildscript {
    ext.kotlin_version = '1.3.0'
    ext.grpc_csharp_generator = '$projectdir/.kinda-nuget/packages/grpc.tools/1.15.0/tools/windows_x64/grpc_csharp_plugin.exe'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
    id "org.jetbrains.kotlin.jvm" version "1.3.0"
}

repositories {
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlin-eap" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

def volitionBuildNumber = System.getenv("BUILD_NUMBER") ?: '0'
def volitionVersion = "0.9.$volitionBuildNumber"
def volitionName = 'volition-api'

allprojects {

    group 'com.empowerops'
    version volitionVersion

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://kotlin.bintray.com/kotlin-eap" }
    }
}

subprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compile 'io.grpc:grpc-netty-shaded:1.15.1'
        compile 'io.grpc:grpc-protobuf:1.15.1'
        compile 'io.grpc:grpc-stub:1.15.1'

        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.0.0"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    kotlin {
    }

    //This is purely for syntax highlighting in intelliJ,
    //this should be done by the gradle/proto plugin, but it isnt because they're dumb.
    sourceSets.main.java.srcDirs += 'build/generated/source/proto/main/java'
    sourceSets.main.java.srcDirs += 'build/generated/source/proto/main/grpc_java'
    javadoc { exclude "com/empowerops/volition/optimizer/**" }
}

project('api'){

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'com.google.protobuf'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven-publish'

    dependencies {
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'
        compile 'javax.annotation:javax.annotation-api:1.3.2'
        compile 'com.google.guava:guava:27.0.1-jre'
    }

    protobuf {

        protoc {
            artifact = "com.google.protobuf:protoc:3.12.3"
        }
        plugins {
            grpc_java {
                artifact = 'io.grpc:protoc-gen-grpc-java:1.15.1'
            }
            grpc_csharp {
                path = file("$projectDir/../.kinda-nuget/packages/grpc.tools/1.15.0/tools/windows_x64/grpc_csharp_plugin.exe").absolutePath
                //TODO: consider dynamically pulling from nuget
                //https://github.com/Ullink/gradle-nuget-plugin
                // --also note, he has an msbuild plugin for gradle
            }
            grpc_cpp {
                path = "$projectDir/../grpc_x86-windows/tools/grpc/grpc_cpp_plugin.exe"
            }
        }

        generateProtoTasks {
            all().each { task ->
                task.plugins {
                    grpc_java {}
                    grpc_csharp {}
                    grpc_cpp {}
                }
                task.builtins {
                    java { }
                    csharp {}
                    cpp {}
                }
            }
        }
    }

    jar {
        baseName = volitionName
        manifest {
            attributes 'Specification-Title': 'Volition API'
            attributes 'Specification-Version': volitionVersion
            attributes 'Specification-Vendor': 'Empower Operations Corp'
            attributes 'Implementation-Version': volitionBuildNumber
            attributes 'Implementation-Vendor': 'Empower Operations Corp'
        }
    }
    
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        baseName = volitionName
        from sourceSets.main.allSource
    }
    
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        baseName = volitionName
        from javadoc.destinationDir
    }
    
    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            Production(MavenPublication){
                groupId 'com.empowerops'
                artifactId volitionName
                version volitionVersion
                
                //blargwargl: we need to express transitive deps here.
                // see this: https://docs.gradle.org/current/userguide/publishing_maven.html
                
            }
        }
    }

    bintray {
        user = 'empower-operations-team'
        key = project.properties.bintrayEmpoweropsKey
        pkg {
            repo = 'maven'
            name = volitionName
            userOrg = 'empower-operations-team'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/EmpowerOperations/volition.git'

            version {
                name = volitionVersion
                desc = 'Empower Operations Volition API'
                released  = new Date()
            }
        }
        configurations = ['archives']
        publications = ['Production']
    }

}

project('oasis-reference'){
    dependencies {
        compile project(":api")
        compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.30.2'
        compile 'no.tornado:tornadofx:1.7.17'
        compile 'info.picocli:picocli:3.9.5'
        compile group: 'org.funktionale', name: 'funktionale-all', version: '1.2'
        compile files("babel-0.15.jar")
        implementation 'org.antlr:antlr4-runtime:4.8-1'
        compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-collections-immutable', version: '0.1'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
        testCompile 'org.assertj:assertj-core:3.11.1'
    }


    task copyToLib(type: Copy) {
        from configurations.runtime
        into "$buildDir/deps"
    }

    assemble.dependsOn copyToLib
    
    jar {
        manifest {
            attributes 'Main-Class': 'com.empowerops.volition.ref_oasis.OptimizerCLIKt'
            attributes 'Specification-Title': 'Volition Reference Optimizer'
            attributes 'Specification-Version': volitionVersion
            attributes 'Specification-Vendor': 'Empower Operations Corp'
            attributes 'Implementation-Version': volitionBuildNumber
            attributes 'Implementation-Vendor': 'Empower Operations Corp'
        }
    }

    task deliverable(type: Zip, dependsOn: assemble) {
        from "$buildDir/libs"
        from "$buildDir/deps"
        from "$projectDir"
        include "*.exe"
        include "*.jar"
        archiveName "optimizer-reference-${version}.zip"
        destinationDir(file("$buildDir/deliverable"))
    }
    
    test {
        useJUnitPlatform()
    }
}